import ru.alexeypan.wordcards.*

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion Versions.compileSdk
  defaultConfig {
    applicationId ApplicationId.id
    minSdkVersion Versions.minSdk
    targetSdkVersion Versions.targetSdk
    versionCode Release.versionCode
    versionName Release.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  signingConfigs {
    debug {
      storeFile rootProject.file('debug.keystore')
      keyAlias 'androiddebugkey'
      keyPassword 'android'
      storePassword 'android'
    }
  }
  buildTypes {
    debug {
      minifyEnabled false
      versionNameSuffix "-dev"
    }
    travis.initWith(buildTypes.debug)
    travis {
      minifyEnabled true
      shrinkResources true

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      matchingFallbacks = ['debug', 'release']
    }
  }
  lintOptions {
    if (project.hasProperty('envTravis')) {
      abortOnError false
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation Libs.kotlin
  implementation project (Modules.wordlistApi)
  implementation project (Modules.wordlistImpl)

  implementation Libs.Androidx.appcompat

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
